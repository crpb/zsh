#compdef uscan

_arguments \
  '(--report --safe)'{--report,--safe}'[report but do not download]' \
  '(--download -d)'{--download,-d}'[download new upstream release(default)]' \
  '(--force-download -dd)'{--force-download,-dd}'[download new upstream even is up-to-date w/o overwrite]' \
  '(--overwrite-download -ddd)'{--overwrite-download,-ddd}'[download new upstream even is up-to-date and overwrite]' \
  '(--verbose -v --no-verbose)'{--verbose,-v}'[give verbose output]' \
  '(--debug -vv)'{--debug,-vv}'[dump downloaded pages to stdout]' \
  '(--extra-debug -vvv)'{--extra-debug,-vvv}'[also report remote content during "search" step]' \
  '(--pasv --no-pasv)--pasv[force passive mode for ftp]' \
  '(--pasv --no-pasv)--no-pasv[do not use passive mode for ftp]' \
  '(--symlink --no-symlink)--symlink[make orig.tar.gz symlinks]' \
  '(--symlink --no-symlink)--no-symlink[do not make orig.tar.gz symlinks]' \
  '(--verbose --no-verbose)--no-verbose[do not give verbose output]' \
  '--check-dirname-level:level:((0\:never\ check\ the\ directory\ name 1\:only\ check\ directory\ name\ if\ we\ had\ to\ change\ directory 2\:always\ check\ the\ directory\ name))' \
  '--check-dirname-regex:perl regex' \
  '(--no-conf --noconf)'{--no-conf,--noconf}'[do not read any configuration files]' \
  '(--signature --no-signature --skip-signature)--signature[download signature and verify (default)]' \
  '(--signature --no-signature --skip-signature)--no-signature[do not download signature but verify]' \
  '(--signature --no-signature --skip-signature)--skip-signature[do not bother with signatues]' \
  '--download-version[specify upstream version to match]:version' \
  '--download-debversion[specify debian package version to match the release]:debversion' \
  '--download-current-version[download currently packaged version]' \
  '--destdir[path to download to]:_files -/' \
  '--package[specify package name]:package' \
  '--watchfile[specify watch file]:watchfile:_files' \
  '--base[disable site specific redirects]' \
  '--no-exclusion[disable automatic exclusion of files]' \
  '--timeout[specify request timeout (default 20)]:timeout:_numbers -u seconds' \
  '(--user-agent --useragent)'{--user-agent,--useragent}'[override default user agent]:useragent' \
  '(--dehs --no-dehs)--dehs[send dehs output(xml) to stdout and other output to stderr]' \
  '(--dehs --no-dehs)--no-dehs[use traditional output (default)]' \
  '--log[record md5sum changes]' \
  '--help[display help information]' \
  '--version[display version information]'
